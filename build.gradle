import com.github.spotbugs.SpotBugsTask
import com.netflix.spinnaker.gradle.codestyle.SpinnakerCodeStylePlugin

buildscript {
  ext {
    versions = [
      lombok: '1.18.10',
      resilience4j: '1.1.0',
      awsSdkVersion: '1.11.688',
      kork: '6.22.1',
      guava: '28.1-jre',
      springBoot: springBootVersion
    ]
  }

  repositories {
    maven {
      url "https://dl.bintray.com/spinnaker/gradle"
    }
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }

  dependencies {
    classpath "org.owasp:dependency-check-gradle:5.2.4"
    classpath "com.github.spotbugs:spotbugs-gradle-plugin:3.0.0"
    classpath 'com.netflix.spinnaker.gradle:spinnaker-dev-plugin:7.1.2'
    classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4"
  }
}

plugins {
  id "io.spring.dependency-management" version "1.0.8.RELEASE"
  id "com.github.kt3k.coveralls" version "2.9.0"
}

apply from: 'gradle/owasp-dependency-check.gradle'

allprojects {
  apply plugin: 'java'
  apply plugin: 'groovy'
  apply plugin: 'java-library'
  apply plugin: 'jacoco'
  apply plugin: SpinnakerCodeStylePlugin

  apply plugin: 'com.github.spotbugs'

  dependencies {
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.10.1'
  }

  spotbugs {
    toolVersion = '3.1.12'
    sourceSets = [ sourceSets.main ]
    excludeFilter = file("${rootProject.projectDir}/findbugs-supressions.xml")
  }

  // To generate an HTML report instead of XML
  tasks.withType(SpotBugsTask) {
    reports.xml.enabled = false
    reports.html.enabled = true
  }

  repositories {
    jcenter()
    mavenCentral()
  }

  jacoco {
    toolVersion = "0.8.5"
  }
}

subprojects {
  apply plugin: 'io.spring.dependency-management'

  apply from: file("${rootProject.projectDir}/gradle/bintray.gradle")

  sourceCompatibility = '11'

  sourceSets {
    integrationTest {
      java {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
        srcDir file('src/integration-test/java')
        srcDir file('src/integration-test/groovy')
      }
      resources.srcDir file('src/integration-test/resources')
    }
  }

  configurations {
    integrationTestCompile.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
  }

  //noinspection GroovyAssignabilityCheck
  task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
  }

  dependencyManagement {
    imports {
      mavenBom("org.springframework.boot:spring-boot-dependencies:${versions.springBoot}")
    }
  }

  dependencies {
    // Lombok
    compileOnly "org.projectlombok:lombok:${versions.lombok}"
    annotationProcessor "org.projectlombok:lombok:${versions.lombok}"

    // common test deps
    testImplementation 'org.codehaus.groovy:groovy-all:2.5.7'
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
    testImplementation 'junit:junit:4.12'
    testImplementation group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
    testImplementation 'com.openpojo:openpojo:0.8.13'
  }

  test {
    testLogging {
      events "passed", "skipped", "failed"
    }
  }

  integrationTest {
    testLogging {
      showStandardStreams = true
    }
  }
}


List<String> blackList = [
  'cerberus-api-tests'
]
def publishedProjects = subprojects.findAll { !blackList.contains(it.path) }

task jacocoMerge(type: JacocoMerge) {
  publishedProjects.each { subproject ->
    executionData subproject.tasks.withType(Test)
  }
  doFirst {
    executionData = files(executionData.findAll { it.exists() })
  }
}

task aggregatedJacocoReport(type: JacocoReport, group: 'Coverage reports') {
  description = 'Generates an aggregate report from all subprojects'
  dependsOn publishedProjects.test, jacocoMerge

  additionalSourceDirs.from = files(publishedProjects.sourceSets.main.allSource.srcDirs)
  sourceDirectories.from = files(publishedProjects.sourceSets.main.allSource.srcDirs)
  classDirectories.from = files(publishedProjects.sourceSets.main.output)
  executionData jacocoMerge.destinationFile

  reports {
    html.enabled = true // human readable
    xml.enabled = true // required by coveralls
  }
}

coveralls {
  sourceDirs = publishedProjects.sourceSets.main.allSource.srcDirs.flatten()
  jacocoReportPath = "${buildDir}/reports/jacoco/aggregatedJacocoReport/aggregatedJacocoReport.xml"
}

configurations {
  antJUnit
}

dependencies {
  antJUnit 'org.apache.ant:ant-junit:1.9.7'
}

// Compile all the test results into a single one.
task aggregatedJunitXml {
  ant.taskdef(name: 'junitreport', classname: 'org.apache.tools.ant.taskdefs.optional.junit.XMLResultAggregator', classpath: configurations.antJUnit.asPath)
  dependsOn subprojects*.test
  doFirst {
    ///reports/jacoco/test/jacocoTestReport.xml
    mkdir "$buildDir/reports/jacoco/test"
    ant.junitreport(todir: "$buildDir/reports/jacoco") {
      subprojects.each {
        if (it.testResultsDir.exists()) {
          fileset(dir: it.testResultsDir)
        }
      }
    }
  }
}

task aggregatedJunitHtml(type: TestReport) {
  dependsOn subprojects*.test
  destinationDir = file("$buildDir/reports/allTests")
  // Include the results from the `test` task in all sub projects
  reportOn subprojects*.test
}

task generateAggregatedReports(dependsOn: [
  aggregatedJunitXml,
  aggregatedJunitHtml,
  aggregatedJacocoReport,
  dependencyCheckAggregate
]) {
  doLast {
    println "Finished generating aggregated reports"
  }
}

defaultTasks ':cerberus-web:bootRun'
