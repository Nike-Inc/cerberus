buildscript {
  ext {
    versions = [
      lombok: '1.18.10',
      resilience4j: '1.1.0',
      awsSdkVersion: '1.11.688',
      kork: '6.22.1',
      springBoot: springBootVersion
    ]
  }
}

plugins {
  id "io.spring.dependency-management" version "1.0.8.RELEASE"
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'groovy'
  apply plugin: 'jacoco'
  apply plugin: 'java-library'
  apply plugin: 'io.spring.dependency-management'

  sourceCompatibility = '11'

  repositories {
    jcenter()
    mavenCentral()
  }

  sourceSets {
    integrationTest {
      java {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
        srcDir file('src/integration-test/java')
        srcDir file('src/integration-test/groovy')
      }
      resources.srcDir file('src/integration-test/resources')
    }
  }

  configurations {
    integrationTestCompile.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
  }

  //noinspection GroovyAssignabilityCheck
  task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
  }

  dependencyManagement {
    imports {
      mavenBom("org.springframework.boot:spring-boot-dependencies:${versions.springBoot}")
    }
  }

  dependencies {
    // Lombok
    compileOnly "org.projectlombok:lombok:${versions.lombok}"
    annotationProcessor "org.projectlombok:lombok:${versions.lombok}"

    // common test deps
    testImplementation 'org.codehaus.groovy:groovy-all:2.5.7'
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
    testImplementation 'junit:junit:4.12'
    testImplementation group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
    testImplementation 'com.openpojo:openpojo:0.8.13'
  }

  jacoco {
    toolVersion = "0.8.5"
  }

  jacocoTestReport {
    reports {
      xml.enabled false
      csv.enabled false
      html.destination file("${buildDir}/jacocoHtml")
    }

    // needed for report - if you use source sets it'll bring in all deps into the report
    def classTree = fileTree(dir: 'build/classes/java/main', include: 'com/nike/cerberus/**')
    // maybe omit classes from the report
    classTree.exclude '**/*App.class'
    classDirectories.from = classTree

    // needed for source highlight
    sourceDirectories.from = files(sourceSets.main.allSource.srcDirs)
  }

  test {
    testLogging {
      events "passed", "skipped", "failed"
    }
  }

  integrationTest {
    testLogging {
      showStandardStreams = true
    }
  }
}

defaultTasks ':cerberus-web:bootRun'
