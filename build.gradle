/*
 * Copyright (c) 2020 Nike, inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License")
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


import com.github.spotbugs.snom.SpotBugsReport
import com.netflix.spinnaker.gradle.codestyle.SpinnakerCodeStylePlugin

buildscript {
  ext {
    versions = [
      lombok: '1.18.18',
      resilience4j: '1.7.0',
      awsSdkVersion: '1.11.948',
      kork: '7.99.0',
      guava: '30.1-jre',
      springBoot: '2.4.2',
    ]
  }
  ext['groovy.version'] = '3.0.7'

  repositories {
    maven {
      url "https://dl.bintray.com/spinnaker/gradle"
    }
    maven {
      url "https://plugins.gradle.org/m2/"
    }
    google()
  }

  dependencies {
    classpath "org.owasp:dependency-check-gradle:6.1.5"
    classpath 'com.netflix.spinnaker.gradle:spinnaker-dev-plugin:7.11.6'
    classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5"
  }
}

plugins {
  id "io.spring.dependency-management" version "1.0.11.RELEASE"
  id "com.github.nbaztec.coveralls-jacoco" version "1.2.11"
  id "com.github.spotbugs" version "4.6.0"
}

apply from: 'gradle/owasp-dependency-check.gradle'

allprojects {
  apply plugin: 'java'
  apply plugin: 'groovy'
  apply plugin: 'java-library'
  apply plugin: 'jacoco'
  apply plugin: SpinnakerCodeStylePlugin
  apply plugin: 'com.github.spotbugs'

  dependencies {
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.11.0'
  }

  spotbugs {
    toolVersion = '4.2.0'
    excludeFilter = file("${rootProject.projectDir}/findbugs-supressions.xml")
  }

  spotbugsMain {
    reports(({
      html {
        enabled = true
        destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
        stylesheet = 'fancy-hist.xsl'
      }
      xml {
        enabled = false
      }
    } as Closure<NamedDomainObjectContainer<? extends SpotBugsReport>>))
  }

  spotbugsTest {
    reports(({
      html {
        enabled = true
        destination = file("$buildDir/reports/spotbugs/test/spotbugs.html")
        stylesheet = 'fancy-hist.xsl'
      }
      xml {
        enabled = false
      }
    } as Closure<NamedDomainObjectContainer<? extends SpotBugsReport>>))
  }



  repositories {
    jcenter()
    mavenCentral()
    google()
  }

  jacoco {
    toolVersion = "0.8.5"
  }
}

subprojects {
  apply plugin: 'io.spring.dependency-management'

  apply from: file("${rootProject.projectDir}/gradle/bintray.gradle")

  sourceCompatibility = '11'

  sourceSets {
    integrationTest {
      java {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
        srcDir file('src/integration-test/java')
      }
      groovy {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
        srcDir file('src/integration-test/groovy')
      }
      resources.srcDir file('src/integration-test/resources')
    }
  }

  configurations {
    integrationTestCompile.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
  }

  //noinspection GroovyAssignabilityCheck
  task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
  }

  dependencyManagement {
    imports {
      mavenBom("org.springframework.boot:spring-boot-dependencies:${versions.springBoot}")
    }
    dependencies {
      //upgrading tomcat manually until Spring-Boot 2.3.5 is released to resolve a security vulnerability
      dependency 'org.apache.tomcat.embed:tomcat-embed-core:9.0.41'
      dependency 'org.apache.tomcat.embed:tomcat-embed-websocket:9.0.41'
    }
  }

  dependencies {
    // Lombok
    compileOnly "org.projectlombok:lombok:${versions.lombok}"
    annotationProcessor "org.projectlombok:lombok:${versions.lombok}"

    // common test deps
    testImplementation 'org.codehaus.groovy:groovy-all:3.0.7'
    testImplementation 'org.spockframework:spock-core:2.0-M4-groovy-3.0'
    testImplementation 'junit:junit:4.13.1'
    testImplementation group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
    testImplementation 'com.openpojo:openpojo:0.8.13'
  }

  test {
    testLogging {
      events "passed", "skipped", "failed"
    }
  }

  integrationTest {
    testLogging {
      showStandardStreams = true
    }
  }
}

List<String> blackList = [
  'cerberus-api-tests'
]
def publishedProjects = subprojects.findAll { !blackList.contains(it.path) }

task jacocoMerge(type: JacocoMerge) {
  publishedProjects.each { subproject ->
    executionData subproject.tasks.withType(Test)
  }
  doFirst {
    executionData = files(executionData.findAll { it.exists() })
  }
}

task aggregatedJacocoReport(type: JacocoReport, group: 'Coverage reports') {
  description = 'Generates an aggregate report from all subprojects'
  dependsOn publishedProjects.test, jacocoMerge

  additionalSourceDirs.from = files(publishedProjects.sourceSets.main.allSource.srcDirs)
  sourceDirectories.from = files(publishedProjects.sourceSets.main.allSource.srcDirs)
  classDirectories.from = files(publishedProjects.sourceSets.main.output)
  executionData jacocoMerge.destinationFile

  reports {
    html.enabled = true // human readable
    xml.enabled = true // required by coveralls
  }
}

coverallsJacoco {
  reportSourceSets = publishedProjects.sourceSets.main.allSource.srcDirs.flatten()
  reportPath = "${buildDir}/reports/jacoco/aggregatedJacocoReport/aggregatedJacocoReport.xml"
}

configurations {
  antJUnit
}

dependencies {
  antJUnit 'org.apache.ant:ant-junit:1.10.9'
}

// Compile all the test results into a single one.
task aggregatedJunitXml {
  ant.taskdef(name: 'junitreport', classname: 'org.apache.tools.ant.taskdefs.optional.junit.XMLResultAggregator', classpath: configurations.antJUnit.asPath)
  dependsOn subprojects*.test
  doFirst {
    ///reports/jacoco/test/jacocoTestReport.xml
    mkdir "$buildDir/reports/jacoco/test"
    ant.junitreport(todir: "$buildDir/reports/jacoco") {
      subprojects.each {
        if (it.testResultsDir.exists()) {
          fileset(dir: it.testResultsDir)
        }
      }
    }
  }
}

task aggregatedJunitHtml(type: TestReport) {
  dependsOn subprojects*.test
  destinationDir = file("$buildDir/reports/allTests")
  // Include the results from the `test` task in all sub projects
  reportOn subprojects*.test
}

task generateAggregatedReports(dependsOn: [
  aggregatedJunitXml,
  aggregatedJunitHtml,
  aggregatedJacocoReport,
  dependencyCheckAggregate
]) {
  doLast {
    println "Finished generating aggregated reports"
  }
}

task aggregatedTest {
  dependsOn subprojects*.test
}

task aggregatedCheck {
  dependsOn subprojects*.check
}

task aggregatedClean {
  dependsOn subprojects*.clean
}

defaultTasks ':cerberus-web:bootRun'
